// Top-level build file where you can add configuration options common to all sub-projects/modules.

ext {
    lib_group = "me.buck"
    lib_version = "0.2.2"
    lib_artifactId_auto_receiver = "auto-receiver"
    lib_artifactId_auto_receiver_annotation = "auto-receiver-annotation"
    lib_artifactId_auto_receiver_compiler = "auto-receiver-compiler"
}

buildscript {
    repositories {
        google()
        jcenter()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath 'digital.wup:android-maven-publish:3.6.2'
        classpath 'com.novoda:bintray-release:0.9.2'
    }
}

allprojects {

    repositories {
        maven {url 'https://dl.bintray.com/buck/maven'}
        google()
        jcenter()
    }
}

void customPom(Object pom) {
    pom.withXml {
        Node root = asNode()
        root.get("dependencies").get(0).get("dependency").each {
            Node groupIdNode = it.get("groupId").get(0)
            Node artifactIdNode = it.get("artifactId").get(0)
            Node versionNode = it.get("version").get(0)
            if (artifactIdNode.children().get(0) == "auto-receiver-annotation") {
                def groupId = new Node(null, "groupId", lib_group)
                def version = new Node(null, "version", lib_version)
                groupIdNode.replaceNode(groupId)
                versionNode.replaceNode(version)
            }
        }
    }
}

task clean(type: Delete) {
    delete "repo"
    delete rootProject.buildDir
}

task cleanBuildPublish(type: GradleBuild) {
    tasks = [
            'clean',
            ':auto-receiver:clean',
            ':auto-receiver:build',
            ':auto-receiver:publish',
            ':auto-receiver:bintrayUpload',
            ':auto-receiver-annotation:clean',
            ':auto-receiver-annotation:build',
            ':auto-receiver-annotation:publish',
            ':auto-receiver-annotation:bintrayUpload',
            ':auto-receiver-compiler:clean',
            ':auto-receiver-compiler:build',
            ':auto-receiver-compiler:publish',
            ':auto-receiver-compiler:bintrayUpload'
    ]
}





